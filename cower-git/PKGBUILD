# Maintainer: Dave Reisner <d@falconindy.com>

pkgname=cower-git
pkgver=16+11+g00d6f92
pkgrel=1
pkgdesc='A simple AUR agent with a pretentious name'
arch=('i686' 'x86_64')
url="http://github.com/falconindy/cower"
license=('MIT')
depends=('curl' 'pacman' 'yajl' )
makedepends=('git' 'perl')
conflicts=('cower')
provides=('cower')
source=('git+https://github.com/falconindy/cower')
sha256sums=('SKIP')

pkgver() {
  cd cower
  git describe | sed 's/-/+/g'
}

build() {
  make -C cower
}

package() {
  make -C cower PREFIX=/usr DESTDIR="$pkgdir" install
}
